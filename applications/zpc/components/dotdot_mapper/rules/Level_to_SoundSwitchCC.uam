// Sound Switch Command Class
def zwSOUND_SWITCH_CONFIGURED_VOLUME 0x7902
def zwSOUND_SWITCH_TONE_INFO_IDENTIFIER 0x7903
def zwSOUND_SWITCH_TONE_NUMBER 0x7904
def zwSOUND_SWITCH_TONE_PLAY 0x7908

// Additional read-only Level cluster properties
def zbLEVEL_CLUSTER_LEVEL   0x00080000
def zbLEVEL_CLUSTER_MIN_LEVEL 0x00080002
def zbLEVEL_CLUSTER_MAX_LEVEL 0x00080003
def zbLEVEL_CLUSTER_FREQ 0x00080004
def zbLEVEL_CLUSTER_FREQ_MIN 0x00080005
def zbLEVEL_CLUSTER_FREQ_MAX 0x00080006
def zbLEVEL_CLUSTER_TRANSITION_TIME 0x00080010

// Constants
def min_level 0
def max_level 100

def zwave_no_sound_switch (e'zwSOUND_SWITCH_TONE_PLAY == 0)

// 25 is higher priority than other generic switches
scope 25  chain_reaction(0) {
  // Min and max volume
  r'zbLEVEL_CLUSTER_MIN_LEVEL = 
    if (zwave_no_sound_switch) undefined
    if (e'zwSOUND_SWITCH_CONFIGURED_VOLUME) min_level undefined
  r'zbLEVEL_CLUSTER_MAX_LEVEL = 
    if (zwave_no_sound_switch) undefined
    if (e'zwSOUND_SWITCH_CONFIGURED_VOLUME) max_level undefined

  // Tone number 
  r'zbLEVEL_CLUSTER_FREQ_MIN = 
    if (zwave_no_sound_switch) undefined
    if (e'zwSOUND_SWITCH_TONE_NUMBER) 1 undefined
  r'zbLEVEL_CLUSTER_FREQ_MAX = 
    if (zwave_no_sound_switch) undefined
    if (e'zwSOUND_SWITCH_TONE_NUMBER) r'zwSOUND_SWITCH_TONE_NUMBER undefined

  // Map current level to configured volume 
  // zwave -> ucl
  r'zbLEVEL_CLUSTER_LEVEL = 
    if (zwave_no_sound_switch) undefined
    r'zwSOUND_SWITCH_CONFIGURED_VOLUME

  d'zbLEVEL_CLUSTER_LEVEL = 
    if (zwave_no_sound_switch) undefined
    d'zwSOUND_SWITCH_CONFIGURED_VOLUME
  // ucl -> zwave
  d'zwSOUND_SWITCH_CONFIGURED_VOLUME = 
    if (zwave_no_sound_switch) undefined
    d'zbLEVEL_CLUSTER_LEVEL
  r'zwSOUND_SWITCH_CONFIGURED_VOLUME = 
    if (zwave_no_sound_switch) undefined
    r'zbLEVEL_CLUSTER_LEVEL

  // Map frequency to current tone identifier
  // zwave -> ucl
  r'zbLEVEL_CLUSTER_FREQ = 
    if (zwave_no_sound_switch) undefined
    r'zwSOUND_SWITCH_TONE_INFO_IDENTIFIER
  d'zbLEVEL_CLUSTER_FREQ = 
    if (zwave_no_sound_switch) undefined
    d'zwSOUND_SWITCH_TONE_INFO_IDENTIFIER
  // ucl -> zwave
  d'zwSOUND_SWITCH_TONE_INFO_IDENTIFIER = 
    if (zwave_no_sound_switch) undefined
    d'zbLEVEL_CLUSTER_FREQ
  r'zwSOUND_SWITCH_TONE_INFO_IDENTIFIER = 
    if (zwave_no_sound_switch) undefined
    r'zbLEVEL_CLUSTER_FREQ

  // Required to enable move command 
  d'zbLEVEL_CLUSTER_TRANSITION_TIME = 
    if (zwave_no_sound_switch) undefined
    if (e'zwSOUND_SWITCH_TONE_NUMBER) 0 undefined
  r'zbLEVEL_CLUSTER_TRANSITION_TIME = 
    if (zwave_no_sound_switch) undefined
    if (e'zwSOUND_SWITCH_TONE_NUMBER) 0 undefined
}
